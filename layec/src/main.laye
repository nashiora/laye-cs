/*

// X finished
// ! in progress
// T needs testing, but looks to be implemented

[ ] Full type implementations
    [ ] Rune type (and basic string UTF8 reading associated with it)
[ ] Operator expressions
    [T] Infix ops (+, -, *, /, %, <<, >>, &, |, ~, <, >, ==, !=, <=, >=, and, or)
    [T] Prefix ops (&, *, -, not)
    [X] Explicit type casts
[ ] Structure operations
    [X] Size of structure `sizeof(Type)`
    [ ] Offset of structure element (optional?) `offsetof(Type, field_name)`
    [ ] Structural initializers
    [X] Zero initializers
[ ] Branch structures
    [X] If/else `if (expr) { } else if (expr) { } else { }
    [X] While `while (expr) { } else { }`
    [ ] C-style For `for (binding or expr; expr; expr) { } else { }`
    [ ] Switch statements `switch (expr) { case Constant: <scoped exprs, no break needed> default: <same> }`
[ ] Tagged unions
    [X] Enum syntax `enum EnumName { VariantName }` `enum EnumName { VariantName(int VariantField) }`
    [ ] Switch statement support for enums `switch (expr) { case EnumName::VariantName: <scoped exprs, no break needed> case ::VariantName <same> default: <same> }`
    [X] Union syntax (optional) `enum EnumName { VariantName }` `enum EnumName { VariantName(int VariantField) }`
    [ ] Switch statement support for unions (optional) `switch (expr) { case UnionName::VariantName: <scoped exprs, no break needed> default: <same> }`

*/

// TODO(local): compile `main` into `_laye_start`, overwrite the entry point with actual startup logic to turn this into a string slice
void main(i32 argc, u8 readonly[*] readonly[*] argv)
{
    // program startup logic (if we keep the idea of passing the program args to main like C and friends)
    string[*] argvStorage = memory_allocate(sizeof(string) * argc);
    uint argvCounter = 0;
    while (argvCounter < argc)
    {
        argvStorage[argvCounter] = argv[argvCounter][:strlen(argv[argvCounter])];
        argvCounter = argvCounter + 1;
    }
    string[] args = argvStorage[:argc];
    // end program startup logic

    laye_main(args);
}

void laye_main(string[] args)
{
    printf("Laye stand-alone compiler%cVersion 0.1.0%c%c", 10, 10, 10);

    if (args.length < 2)
    {
        printf("error: no file given%c", 10);
        return;
    }

    diagnostic_bag diagnostics;

    string sourceFilePath = args[1];
    source sourceFile = source_create_from_file(sourceFilePath);
    if (not sourceFile.isValid)
    {
        printf("failed to open file%c", 10);
        return;
    }

    parser_data layeParser;
    parser_init(&layeParser, sourceFile, &diagnostics);

    syntax_node *[dynamic] nodeStorage;
    bool success = parser_read_all_laye_nodes(&layeParser, &nodeStorage);
    //if (not success) { diagnostics_print(diagnostics); return; }

    syntax_node *[] nodes = nodeStorage[:];
    if (nodeStorage.length > 0) dynamic_free(nodeStorage);

    semantic_data layeSemantics;
    semantics_analyze_laye_syntax(&layeSemantics, nodes, &diagnostics);

    //lower_data layeLower;
    //lower_process_laye_syntax(&layeLower, nodes);

    diagnostics_print(diagnostics);
    if (diagnostics.errorCount > 0)
    {
        printf("compilation completed with errors%cno code generated%c", 10, 10);
        return;
    }
}

void diagnostics_print(diagnostic_bag b)
{
    uint count = b.diagnostics.length;

    {
        uint i = 0;
        while (i < count)
        {
            diagnostic d = b.diagnostics[i];

            string locationString = source_location_to_string(diagnostic_source_span_get(d).startLocation);
            string kindString = nameof_variant(d.kind);

            printf("%.*s: %.*s: %.*s%c",
                locationString.length, locationString.data,
                kindString.length, kindString.data,
                d.message.length, d.message.data,
                10);

            i = i + 1;
        }
    }
}
